# Create baidu map control.
createBMap = (container) ->
    map = new BMap.Map(container)
    map.addControl(new BMap.NavigationControl())
    map.addControl(new BMap.ScaleControl())
    map.enableScrollWheelZoom()
    return map

# Display bike location map.
displayBikeLocationMap = (container, userName, bikeName, longitude, latitude) ->
    map = createBMap(container)

    point = new BMap.Point(longitude, latitude)

    map.centerAndZoom(point, 19)

    convertor = new BMap.Convertor()
    convertor.translate([point], 1, 5, (data) ->
      if data.status == 0
        icon = new BMap.Icon("<%= asset_url('bike_marker.gif') %>", new BMap.Size(75, 65)) 
        convertedPoint = data.points[0]
        marker = new BMap.Marker(convertedPoint, {icon: icon})

        label = new BMap.Label(userName + "的" + bikeName + "的位置",
                               {offset: new BMap.Size(-30, 30)})
        label.setStyle(
               color: "rgb(255,65,54)"
               fontWeight: "bold"
               fontSize: "18px"
               borderStyle: "none")

        marker.setLabel(label)
        map.addOverlay(marker)
        map.centerAndZoom(convertedPoint, 19)
    )

# Display bike track map.
displayBikeTrackMap = (container, locations) ->
    map = createBMap(container)

    count = locations.length

    return if count == 0

    points = new Array()

    for location in locations
        point = new BMap.Point(location.longitude, location.latitude)
        points.push(point)

    map.centerAndZoom(points[count - 1], 19)

    convertor = new BMap.Convertor()
    convertor.translate(points, 1, 5, (data)->
      if data.status == 0
        polyline = new BMap.Polyline(data.points,
            strokeColor: "#FF264F",
            strokeWeight: 4,
            strokeOpacity: 0.8)
        map.addOverlay(polyline)
        polyline.enableEditing()
        map.centerAndZoom(data.points[count - 1], 17)
    )

# Set bike maps size.
setBikeMapsSize = ->
  mapHeight = window.innerHeight - 200 
  $("#bike-location-map").height(mapHeight)
  $("#bike-track-map").height(mapHeight)

# Create map control and set event handlers.
$ ->
  setBikeMapsSize()

  # Default display bike location map.
  displayBikeLocationMap("bike-location-map",
    gon.userName,
    gon.bikeName,
    gon.longitude,
    gon.latitude)

  isBikeTrackMapShown = false
  $('a[href="#map-track-tab"]').on 'shown.bs.tab', ->
      if !isBikeTrackMapShown
          displayBikeTrackMap("bike-track-map", gon.travelTrackHistories)
          isBikeTrackMapShown = true
